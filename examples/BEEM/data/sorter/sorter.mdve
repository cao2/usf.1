default(SCENARIO, 1)

define(FWD, 1)
define(REV, 2)
define(SHORT, 3)
define(LONG, 4)

byte token = 0;
byte belt1_moving = FWD;
byte belt2_moving = 0;

bool arm_kicking = 0;
bool light_sensor_level = 0;
bool button_pressed = 0;

bool put_short_brick = 0;
bool put_long_brick = 0;

byte requests = 0;
byte timer = 0;
byte brick = 0;

// artificial bounds
define(MAX_REQ, 5)
define(MAX_TIMER, 8)

process ButtonControler {
 state BC;
 init BC;
 trans
  BC -> BC { guard token == 0 && button_pressed == 0; effect token = 1; },
  BC -> BC { guard token == 0 && button_pressed == 1 && requests < MAX_REQ; effect button_pressed = 0, requests = requests + 1, token = 1; },
  BC -> BC { guard token == 0 && button_pressed == 1 && requests == MAX_REQ; effect button_pressed = 0,  token = 1; };
}

process ArmControler {
 state AC0, AC1;
 init AC0;
 trans
  AC0 -> AC0 { guard token == 1 && not (brick == LONG && timer > 3); effect token = 2; },
  AC0 -> AC1 { guard token == 1 && brick == LONG && timer > 3;
		effect brick = 0, arm_kicking = 1, belt1_moving = 0, token = 2; },
  AC1 -> AC0 { guard token == 1; effect arm_kicking = 0, belt1_moving = 1, token = 2; };
}

process Belt2Controler {
 byte t1 = 0;
 state B2C0, tmp, B2C1;
 init B2C0;
 trans
  B2C0 -> B2C0 { guard token == 2 && not (brick == SHORT && timer > 7); effect token = 3;},
  B2C0 -> tmp { guard token == 2 && (brick == SHORT && timer > 7); effect t1 = 0; },
  tmp -> B2C1 { guard requests > 0; effect belt2_moving = REV, token = 3; },
  tmp -> B2C1 { guard requests == 0; effect belt2_moving = FWD, token = 3; },
  B2C1 -> B2C1 { guard token == 2 && t1 < 4; effect t1 = t1 + 1, token = 3; },
  B2C1 -> B2C0 { guard token == 2 && t1 == 4; effect token = 3; };  
}

process LightControler {
 byte x;
 state LC0, tmp, LC1;
 init LC0;
 trans
  LC0 -> LC0 { guard token == 3 && light_sensor_level == 0; effect token = 4; },
  LC0 -> LC1 { guard token == 3 && light_sensor_level == 1; effect x = 1, token = 4; },
  LC1 -> LC1 { guard token == 3 && light_sensor_level == 1; effect x = x + 1, token = 4; },
  LC1 -> tmp { guard token == 3 && light_sensor_level == 0; effect timer = 0;},
  tmp -> LC0 { guard x <= 2; effect brick = SHORT, token = 4; }, 
  tmp -> LC0 { guard x > 2; effect brick = LONG, token = 4; };    
}

process TimerStep {
 state T0;
 init T0;
 trans
  T0 -> T0 { guard token == 4 && timer < MAX_TIMER; effect timer = timer + 1, token = 5; },
  T0 -> T0 { guard token == 4 && timer == MAX_TIMER; effect token = 5; };
}

process User {
 byte t2;
 state U0, Wait;
 init U0;
 trans
  U0 -> U0 { guard token == 5; effect token = 6; }, 
  U0 -> U0 { guard token == 5; effect button_pressed = 1, token = 6; },
  U0 -> Wait { guard token == 5; effect put_short_brick = 1, t2 = 1, token = 6; }, 
  U0 -> Wait { guard token == 5; effect put_long_brick = 1, t2 = 3, token = 6; },
  Wait -> Wait { guard token == 5 && t2 > 0; effect t2 = t2 -1, token = 6; },
  Wait -> U0 { guard token == 5 && t2 == 0; effect token = 6; };
}

define(NOT_USED, 0)
define(ON_BELT1, 1)
define(ON_BELT2, 2)
define(OUT_KICKED, 3)
define(OUT_NOT_SELECTED, 4)
define(OUT_SELECTED, 5)

define(short_brick, `process ShortBrick$1 {
 byte location = 0;
 byte position = 0;
 state B;
 init B;
 trans 
 B -> B { guard token == $2 && location == NOT_USED && put_short_brick == 0; effect token = $3; },
 B -> B { guard token == $2 && location == NOT_USED && put_short_brick == 1; effect put_short_brick=0,  location = ON_BELT1, token = $3; },
 
//moving forward
 B -> B { guard token == $2 && location == ON_BELT1 &&
		(position < 3 || position ==4 ||(position > 5 && position < 10))  && belt1_moving == FWD; effect position = position + 1, token = $3; },
 B -> B { guard token == $2 && location == ON_BELT1 && belt1_moving == FWD && position == 3;
		effect light_sensor_level = 1, position = position + 1, token = $3; },
 B -> B { guard token == $2 && location == ON_BELT1 && belt1_moving == FWD && position == 5;
		effect light_sensor_level = 0, position = position + 1, token = $3; },
 B -> B { guard token == $2 && location == ON_BELT1 && position == 10 && arm_kicking == 0  && belt1_moving == FWD;
		effect position = position + 1, token = $3; },
 B -> B { guard token == $2 && location == ON_BELT1 && position == 11 && arm_kicking == 0  && belt1_moving == FWD;
		effect location = ON_BELT2, position = 3, token = $3; },

 // staying
 B -> B { guard token == $2 && location == ON_BELT1 && belt1_moving == 0 && position <10;
		effect token = $3; },
 B -> B { guard token == $2 && location == ON_BELT1 && belt1_moving == 0 && (position ==10 || position ==11) && arm_kicking == 0;
		effect token = $3; },

// arm kicking
 B -> B { guard token == $2 && location == ON_BELT1 && (position == 10 || position ==11) && arm_kicking == 1; effect location = OUT_KICKED, token = $3; },


//Belt2 moving
 B -> B { guard token == $2 && location == ON_BELT2 && belt2_moving == FWD && position < 5;
	 effect position = position + 1, token = $3; }, 
 B -> B { guard token == $2 && location == ON_BELT2 && belt2_moving == REV && position > 0;
	 effect position = position - 1, token = $3; },
 B -> B { guard token == $2 && location == ON_BELT2 && belt2_moving == 0;
	 effect token = $3; }, 
 B -> B { guard token == $2 && location == ON_BELT2 && belt2_moving == FWD && position == 5;
	 effect location = OUT_NOT_SELECTED, token = $3; }, 
 B -> B { guard token == $2 && location == ON_BELT2 && belt2_moving == REV && position == 0;
	 effect location = OUT_SELECTED, token = $3; },
 //Out - just pass token
 B -> B { guard location == OUT_SELECTED || location == OUT_NOT_SELECTED || location == OUT_KICKED; effect token = $3; };
}
')

define(long_brick, `process LongBrick$1 {
 byte location = 0;
 byte position = 0;
 state B, selected;
 init B;
 trans 
 B -> B { guard token == $2 && location == NOT_USED && put_long_brick == 0; effect token = $3; },
 B -> B { guard token == $2 && location == NOT_USED && put_long_brick == 1; effect put_long_brick =0,  location = ON_BELT1, token = $3; },
 
//moving forward
 B -> B { guard token == $2 && location == ON_BELT1 && belt1_moving == FWD &&
	(position == 0 || (position > 1 && position < 5) || (position > 5 && position < 8)) ;
	  effect position = position + 1, token = $3; },
 B -> B { guard token == $2 && location == ON_BELT1 && belt1_moving == FWD && position == 1;
		effect light_sensor_level = 1, position = position + 1, token = $3; },
 B -> B { guard token == $2 && location == ON_BELT1 && belt1_moving == FWD && position == 5;
		effect light_sensor_level = 0, position = position + 1, token = $3; },
 B -> B { guard token == $2 && location == ON_BELT1 && (position >=8 && position <=10) && arm_kicking == 0 && belt1_moving == FWD;
		effect position = position + 1, token = $3; },
 B -> B { guard token == $2 && location == ON_BELT1 && position == 11 && arm_kicking == 0  && belt1_moving == FWD;
		effect location = ON_BELT2, position = 3, token = $3; },

 // staying
 B -> B { guard token == $2 && location == ON_BELT1 && belt1_moving == 0 && position <8;
		effect token = $3; },
 B -> B { guard token == $2 && location == ON_BELT1 && belt1_moving == 0 && (position >=8 || position ==11) && arm_kicking == 0;
		effect token = $3; },

// arm kicking
 B -> B { guard token == $2 && location == ON_BELT1 && position >=8 && arm_kicking == 1; effect location = OUT_KICKED, token = $3; },


//Belt2 moving
 B -> B { guard token == $2 && location == ON_BELT2 && belt2_moving == FWD && position < 5;
	 effect position = position + 1, token = $3; }, 
 B -> B { guard token == $2 && location == ON_BELT2 && belt2_moving == REV && position > 0;
	 effect position = position - 1, token = $3; },
 B -> B { guard token == $2 && location == ON_BELT2 && belt2_moving == 0;
	 effect token = $3; }, 
 B -> B { guard token == $2 && location == ON_BELT2 && belt2_moving == FWD && position == 5;
	 effect location = OUT_NOT_SELECTED, token = $3; }, 
 B -> B { guard token == $2 && location == ON_BELT2 && belt2_moving == REV && position == 0;
	 effect location = OUT_SELECTED, token = $3; },
 //Out - just pass token
 B -> B { guard location == OUT_SELECTED || location == OUT_NOT_SELECTED || location == OUT_KICKED; effect token = $3; };
}
')


ifelse(SCENARIO, 1, `
short_brick(1,6,0)',
SCENARIO, 2, `
long_brick(1,6,0)',
SCENARIO, 3, `
short_brick(1,6,7)
short_brick(2,7,0)
',SCENARIO, 4, `
short_brick(1,6,7)
long_brick(2,7,0)',
SCENARIO, 5, `
long_brick(1,6,7)
long_brick(2,7,0)'
)

system async;