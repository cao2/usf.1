default(N,2)
default(K1,2)
default(K2,5) // should be > 2*K1 to have it correct
define(OFF,255)
define(NULL,255)

byte x, y = NULL;
bool z;
byte t[N] = { myloop(i,0,decr(N), `OFF',`,') };

define(P, `process P_$1 {
state NCS, p2, p3, p4, p5, p6, p7, p8, p9, CS, p12, p13;
init NCS;
trans
// NCS ->  { effect },
 NCS -> p3 { effect x = $1, t[$1] = OFF;},
 p3 -> p4 { guard y == NULL; effect t[$1] = K1; },
 p4 -> p5 { effect y = $1, t[$1] = K1; },

 p5 -> p9 { guard x == $1; effect t[$1] = K1; },
 p9 -> CS { effect z = 1, t[$1] = OFF; },

 p5 -> p6 { guard x != $1; effect t[$1] = K2; }, //delay at p6
 p6 -> p7 { guard t[$1] == 0; effect t[$1] = K1; },
 p7 -> NCS { guard y != $1; effect t[$1] = K1; },
 p7 -> p8 { guard y == $1; effect t[$1] = OFF; },
 p8 -> CS { guard z == 0;},

 CS -> p12 { effect z = 0, t[$1] = K1;},
 p12 -> NCS { guard y != $1; effect t[$1] = OFF; },
 p12 -> p13 { guard y == $1; effect t[$1] = K1; },
 p13 -> NCS { effect y = NULL, t[$1] = OFF; };

}
')

process Timer {
state q;
init q;
trans
 q -> q { guard myloop(i, 0, decr(N), `t[i] != 0',` && ');
effect myloop(i,0,decr(N), `t[i] = (t[i]-1) | ((t[i]==255)*255)',`,'); };
}

forloop(i, 0, decr(N), `P(i)')

system async;